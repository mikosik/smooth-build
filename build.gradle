plugins {
    id 'com.diffplug.spotless' version '7.0.2' apply false
}

subprojects {
    apply plugin: 'com.diffplug.spotless'
    spotless {
        format 'misc', {
            target '*.gradle', '.gitattributes', '.gitignore', "*.g4"
            trimTrailingWhitespace()
            indentWithSpaces()
            endWithNewline()
        }
        antlr4 {
            target '**/*.g4'
            antlr4Formatter()
        }
    }

    tasks.withType(Test) {
        maxParallelForks = Runtime.runtime.availableProcessors()
    }
}

subprojects { subproject ->
    if (subproject.name in [
            'common-testing',
            'common',
            'compiler-frontend',
            'virtual-machine',
            'compiler-backend',
            'evaluator',
            'cli',
            'standard-library',
            'system-test']) {
        apply plugin: 'java'
        java {
            toolchain {
                languageVersion = JavaLanguageVersion.of(21)
            }
        }
        apply plugin: 'com.diffplug.spotless'
        spotless {
            java {
                removeUnusedImports()
                palantirJavaFormat('2.50.0').style("GOOGLE")
            }
        }
    }
}
