plugins {
    id 'application'
    id 'java-test-fixtures'
}

application {
    mainClass = 'org.smoothbuild.cli.Main'
}

dependencies {
    implementation project(':src:common')
    implementation project(':src:antlr-report-matcher')
    implementation project(':src:compiler-frontend')
    implementation project(':src:compiler-backend')
    implementation project(':src:virtual-machine')
    implementation project(':src:evaluator')

    implementation libs.guava
    implementation libs.dagger
    annotationProcessor libs.dagger.compiler
    implementation libs.jakarta.inject
    implementation libs.okio
    implementation libs.picocli

    testFixturesApi project(':src:common')
    testFixturesApi project(':src:cli')
    testFixturesApi testFixtures(project(':src:common'))
    testFixturesApi testFixtures(project(':src:virtual-machine'))
    testFixturesApi testFixtures(project(':src:compiler-frontend'))
    testFixturesApi libs.guava
    testFixturesImplementation libs.dagger
    testFixturesAnnotationProcessor libs.dagger.compiler
    testFixturesImplementation libs.jakarta.inject
    testFixturesApi libs.junit.jupiter.api
    testFixturesApi libs.mockito
    testFixturesApi libs.okio
    testFixturesApi libs.truth

    testImplementation project(':src:common-testing')
    testImplementation project(':src:virtual-machine')
    testImplementation testFixtures(project(':src:common'))
    testImplementation testFixtures(project(':src:virtual-machine'))
    testImplementation testFixtures(project(':src:evaluator'))
    testImplementation libs.guava.testlib
    testRuntimeOnly libs.junit.jupiter.engine
    testImplementation libs.junit.jupiter.api
    testImplementation libs.junit.jupiter.params
    testImplementation libs.mockito
    testImplementation libs.truth
}

def fatJar = tasks.register('fatJar', Jar) {
    manifest {
        attributes = []
    }
    archiveBaseName = 'smooth'
    dependsOn configurations.runtimeClasspath
    dependsOn sourceSets.main.output
    from sourceSets.main.output
    from {
        configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect {
            zipTree(it).matching {
                exclude "META-INF/**", "LICENSE", "module-info.class"
            }
        }
    }
}
tasks.assemble.dependsOn(fatJar)

test {
    jvmArgs "-Xshare:off"  // to silence JVM warning emitted by gradle
}
